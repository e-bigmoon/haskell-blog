---
title: Haskell Quiz No.3 QuickCheck の Arbitrary 型クラス
author: Shinya Yamaguchi
tags: bigmoon, quiz
updated: 2019/12/24
---

## Haskell Quiz No.3

難易度: λ

`propIsDigit` テストをパスするように `Digit` 型の `Arbitrary` インスタンスを定義してみましょう。

```hs
module Quiz3 where

import Test.QuickCheck
import Data.Char (isDigit)

newtype Digit = Digit Char
  deriving Show

propIsDigit :: Digit -> Bool
propIsDigit (Digit c) = isDigit c

instance Arbitrary Digit where
  arbitrary = undefined
```

テストは以下のように実行します。

```sh
$ stack repl --package QuickCheck -- Quiz3.hs
*Quiz3> quickCheck propIsDigit
+++ OK, passed 100 tests.
```

上記のように `OK` になれば (たぶん) 正解です！

ヒント: 生成される値をデバッグしたい場合は `sample'` 関数が便利です。

```sh
*Quiz3> sample' (arbitrary :: Gen Digit)
```

答えは[次回](03-28-quiz-4.html)。

<!--more-->

## はじめに

[前回](./03-25-quiz-2.html)の問題と答えは以下の通りです。

### 問題

難易度: λλ

```hs
foldr (&&) True $ cycle [True, False]
foldl (&&) True $ cycle [True, False]
```

上記の2つの式の挙動の違いを説明してください。

また、なぜそうなるのか考えてみましょう！

### こたえ

```hs
>>> foldr (&&) True $ cycle [True, False]
False

>>> foldl (&&) True $ cycle [True, False]
^CInterrupted.
```

ということで `foldr` は無限リストに対しても停止して値を返しますが、`foldl` は停止せず、値を返さない。

でした。

## Haskell Quiz No.2 の解説

`Haskell` は遅延評価なので `foldr` が無限リストを扱えるということは、知っておいて欲しいです。

また実際のアプリケーションではスペースリークの問題があるため `foldr` < `foldl` < `foldl'` の順で好まれるかもしません。(最適化されるから気にしなくても良いという話も聞いたことありますが、詳しくないので良くわかんないです。)

### cycle

[cycle](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/GHC-List.html#cycle) 関数は `base` パッケージの [GHC.List](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/GHC-List.html#cycle) で定義されています。また [Prelude](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/Prelude.html#v:cycle) モジュールにも含まれています。

以下は実際の定義です。

```hs
cycle :: [a] -> [a]
cycle [] = errorEmptyList "cycle"
cycle xs = xs'
  where xs' = xs ++ xs'
```

この定義を見ればわかるように `cycle` 関数は空リストを与えると実行時エラーになります。

それ以外の場合では与えられたリストを繰り返して無限リストを作ります。

```sh
>>> cycle "Haskell"
"HaskellHaskellHaskell...
```

### Prelude モジュールについて

`Prelude` モジュールは良く使う基本的な関数や型の集まりです。とても良く使うので自動的に `import` されています。

`Prelude` で定義されている関数の `型`・`定義`・`動作` は全部わかるようにしておきましょう。

これは本当に基礎知識です。(Haskell が苦手だなーと思う人はまずは `Prelude` で定義されている関数に慣れてください。それだけでも十分楽しめます。)

また、暗記して覚えることは非常に無駄なのでやめましょう。

何年か `Haskell` を使っていれば自然に覚えますし、感覚的には覚えている関数を書き下すのではなく、`関数名` と `型` または `関数の動作` から実装を導いている感じです。

### Foldable

[Foldable](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/Data-Foldable.html#line-117) 型クラスは `base` パッケージの [Data.Foldable](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/Data-Foldable.html) で定義されています。また [Prelude](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/Prelude.html#t:Foldable) モジュールにも含まれています。

昔の `foldr` はリストに対してのみ適用可能な関数でしたが、比較的最近 `Foldable` クラスのメソッドになりました。そのため、リスト以外でも利用できます。

`Foldable` のクラス定義とリストのインスタンス定義はこんな感じです。(実際にはもっと多くのメソッドがあります)

```hs
class Foldable t where
  foldr :: (a -> b -> b) -> b -> t a -> b
  foldr f z t = appEndo (foldMap (Endo #. f) t) z

instance Foldable [] where
  foldr = List.foldr
```

初学者に優しくない世界になりましたね・・・。なかなか定義にたどり着きません。

`import` 宣言のあたりを見ると `import qualified GHC.List as List` とあるので `List.foldr` は `GHC.List.foldr` だとわかります。

`GHC.List` を `base` パッケージの[一覧](https://www.stackage.org/lts-11.2/package/base-4.10.1.0)から探しても見つかりません。これはわざと利用者に見えないようにしているためです。

実際に [GHC.List](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/GHC-List.html) は存在しますが `{-# OPTIONS_HADDOCK hide #-}` プラグマによって隠されているのです。

`GHC.List` を探してもリストの `foldr` の定義は見つかりません。どうやら [GHC.Base](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/GHC-Base.html) で定義しているようです。

### foldr

[foldr](https://www.stackage.org/haddock/lts-11.2/base-4.10.1.0/src/GHC-Base.html#line-850) の実際の定義は以下の通りです。

```hs
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr k z = go
  where
    go [] = z
    go (y:ys) = y `k` go ys
```

これではわかりずらいので少し変形してみましょう。

```hs
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr k z []     = z
foldr k z (y:ys) = y `k` foldr k z ys
```

変数名もいつもの感じに変えちゃいましょう。

```hs
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr _  e []     = e
foldr f  e (x:xs) = x `f` foldr f e xs -- f x (foldr f e xs) と同じ
```

だいぶ見慣れた形に近づいてきました。

`foldr` (`foldl`) はほどよく抽象化されているので、色々な理解があると思いますので、僕がいつも使っている説明をいくつかご紹介します。

ここからの `foldr`, `foldl` は全てリストについての話です。

#### 直感的な説明

例えば `foldr (+) 0 [1,2,3,4,5]` は以下のように考えることができます。

```hs
[1 , 2 , 3 , 4 , 5]
-- リストの括弧 (`[`, `]`) を外します
1 , 2 , 3 , 4 , 5
-- foldr(ight) なので一番右に 0 を追加します
1 , 2 , 3 , 4 , 5 , 0
-- , を (+) に置き換えます
1 + 2 + 3 + 4 + 5 + 0
-- foldr(ight) なので右結合で括弧をつけていきます
1 + (2 + (3 + (4 + (5 + 0))))

-- 一般化すると
x1 `f` (x2 `f` (x3 `f` (x4 `f` (x5 `f` e))))
```

簡単ですね！この考え方をすれば `foldr` の型を暗記する必要はありません。

`foldr f e xs` の型はこのように考えれば良いのです。

- `f :: a -> b -> ?`。この時 `?` が `a` か `b` か迷いそうですが、上の図で考えれば ``x5 `f` e`` の結果は ``x4 `f` `` の第二引数に再び適用されます。そのため `f :: a -> b -> b` でなければ型が合いません。
- `f` の型がわかれば `e` の型は自然に `b` しか有りえません。
- 当然 `xs` は `x1, x2, x3, ・・・` とリストの形式なので `[a]` しかありえません。
- `foldr` の結果の型は最終的に ``x1 `f` ...`` となるので `f :: a -> b -> b` から `b` です。

以上により `foldr :: (a -> b -> b) -> b -> [a] -> b` が導かれました。

#### universal property を使った説明

普遍性 (`universal property`) の詳しいことは有識者の方に任せるとして

```hs
myFunc []     = e
myFunc (x:xs) = f x (myFunc xs)
```

上記のような再帰パターンの関数は全て `foldr f e` として書けるという性質です。この説明はどちらかと言うと、ベタに再帰で書いた関数を `foldr` を使った形式に書き直す際の理解の手助けとして有用かと思います。

具体例を見ればすぐにわかります。

```hs
sum [] = 0
sum (x:xs) = x + sum xs
```

`sum` 関数は `foldr (+) 0` と同じです。(この時の `e` は `0`, `f` は `(+)` に対応します)

では先程の `cycle` は普遍性を使って `foldr` で書けるの？という疑問になりますよね。

```hs
cycle [] = errorEmptyList "cycle"
cycle xs = xs'
  where xs' = xs ++ xs'
```

先程のパターンと微妙に違うので合わせてみましょう。

また `xs' = cycle xs` なので以下のように変形できます。

```hs
cycle [] = errorEmptyList "cycle"
cycle xs = xs ++ cycle xs
```

さらに変形します。

```hs
cycle []     = errorEmptyList "cycle"
cycle (x:xs) = (x:xs) ++ cycle (x:xs)
```

ここで、最初のパターンと比較しやすいように `cycle` を `myFunc` という名前に変更します。

ついでに `++` の位置も変更しておきます。

```hs
-- cycle
myFunc []     = errorEmptyList "cycle"
myFunc (x:xs) = (++) (x:xs) (myFunc (x:xs))

-- foldr として書き直すことが出来る再帰パターン
myFunc []     = e
myFunc (x:xs) = f x (myFunc xs)
```

どうですか、微妙に違いますよね。

`x` と `xs` に分解しなければいけないのにどちらも `(x:xs)` になってしまっています。

ということで `foldr` を使った `cycle` の定義を普遍性を使って導出することはできません。

### cycle 関数を foldr を使って定義する

普遍性を使った方法では `cycle` を導出することができませんでした。

けど、本当に `foldr` を使って定義することはできないのでしょうか？ (参考: [[Haskell-beginners] folds again -- myCycle](https://mail.haskell.org/pipermail/beginners/2009-March/001184.html))

僕はいつもこのような感じで `foldr` の定義を考えます。`acc` は蓄積変数 (`accumulate variable`) の略です。

`go` の第二引数は常に `foldr` で畳み込んだ結果として考えることができます。

```hs
cycle :: [a] -> [a]
cycle xs = foldr go e undefined
  where
    e        = undefined
    go x acc = undefined
```

少し考えると `xs` を `xs ++ xs ++ xs ...` という形式にできれば良さそうです。

無限リストになるため初期値 `e` は絶対に評価されません。そのため `[]` でも `xs` でも `undefined` でも好きな値が使えそうです。ここでは何となく `xs` にしておきます。

```hs
cycle :: [a] -> [a]
cycle xs = foldr go e undefined
  where
    e        = xs
    go x acc = undefined
```

次に `go` 関数ですが `xs ++ (xs ++ (xs ++ (...)))` となれば良いので、 `go x acc = xs ++ acc` です。

`go` が `cycle` の引数 `xs` を参照している点が通常の使い方と異なる点です。

```hs
cycle :: [a] -> [a]
cycle xs = foldr go e undefined
  where
    e        = xs
    go _ acc = xs ++ acc
```

ここが一番のポイントですが `foldr` に与えるリストは `xs` ではありません。

無限リストを生成するために適当な無限リストを与えます。

```hs
cycle :: [a] -> [a]
cycle xs = foldr go e [0..]
  where
    e        = xs
    go _ acc = xs ++ acc
```

つまりこのような感じです。

```text
0 `go` (1 `go` (2 `go` (3 `go` ...)))
xs ++ (1 `go` (2 `go` (3 `go` ...)))
xs ++ (xs ++ (2 `go` (3 `go` ...)))
xs ++ (xs ++ (xs ++ (3 `go` ...)))
xs ++ (xs ++ (xs ++ (xs ++ ...)))
```

これは完全に `cycle` ですね！

こんな感じで頑張れば `foldr` を使って `cycle` を定義することができます。

ただ、本質的には `unfoldr` を使うべき問題だと思います！(そのうちクイズにします)

### 遅延評価を追ってみよう！

やっと問題の本題です・・・。

`foldr`, `cycle`, [(&&)](https://www.stackage.org/haddock/lts-11.2/ghc-prim-0.5.1.1/src/GHC-Classes.html#%26%26) の定義は以下を使います。

```hs
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr _  e []     = e
foldr f  e (x:xs) = x `f` foldr f e xs

cycle :: [a] -> [a]
cycle xs = xs ++ cycle xs

(&&) :: Bool -> Bool -> Bool
True  && x  =  x
False && _ =  False
```

それでは実際に遅延評価で簡約していきましょう。

```text
foldr (&&) True (cycle [True, False])
  = { cycle を適用 }
foldr (&&) True ([True, False] ++ cycle [True, False])
  = { foldr を適用 }
True && foldr (&&) True ([False] ++ cycle [True, False])
  = { (&&) を適用 }
foldr (&&) True ([False] ++ cycle [True, False])
  = { fodlr を適用 }
False && foldr (&&) True (cycle [True, False])
  = { (&&) を適用 }
False
```

つまり、`(&&)` の定義に秘密があったのです。

`(&&)` 第一引数が `False` であれば第二引数を評価することなく `False` を返します。そのため `False && _ =  False` のような定義になっています。(このような関数を非正格関数 (`non-strict function`) と言います。定義は `f undefined = undefined` であれば正格関数 (`strict function`) です。`(&&)` は第一引数に関しては正格ですが、第二引数に関しては非正格です)

これが `foldr` の場合に計算が停止する理由です。

ではなぜ `foldl` の場合は停止しないのでしょうか？

```hs
foldl :: (a -> b -> a) -> a -> [b] -> a
foldl _ acc []     = acc
foldl f acc (x:xs) = foldl f (acc `f` x) xs
```

`foldl` の場合も同様に簡約してみましょう。

```text
foldl (&&) True (cycle [True, False])
  = { cycle を適用 }
foldl (&&) True ([True, False] ++ cycle [True, False])
  = { foldl を適用 }
foldl (&&) (True && True) ([False] ++ cycle [True, False])
  = { foldl を適用 }
foldl (&&) ((True && True) && False) (cycle [True, False])
  = ...
```

ということで `foldl` はリストの最後にたどり着くまで値を返せないのです。

そのため、無限リストを処理しようとすると停止しなくなります。

## まとめ

いつも解説が長くなってしまいます・・・。

`fold` の融合則とかめちゃめちゃ好きなのでいつかまとめたいですね。

また、`fold` が好きな人には [foldl](https://www.stackage.org/package/foldl) パッケージがオススメです。

以上です。